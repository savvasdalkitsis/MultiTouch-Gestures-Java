buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
    id 'java-library'
    id "com.jfrog.bintray" version "1.8.5"
    id 'maven-publish'
}

def dev_name = 'Savvas Dalkitsis'
def dev_email = 'kurosavvas@gmail.com'
def github_name = 'savvasdalkitsis'
def project_name = 'mac-gestures'
def project_description = 'mac-gestures is a library for accessing multitouch gesture events on macOS for the JVM written in Kotlin'
def project_group = 'com.savvasdalkitsis'
def project_artifact = 'mac-gestures'
def project_version = '0.0.7'
def project_license = 'The Apache Software License, Version 2.0'
def project_license_tag = 'Apache-2.0'
def project_license_url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'

repositories {
    mavenCentral()
}

dependencies {
    implementation(
            "org.jetbrains.kotlin:kotlin-stdlib-jdk8",
    )
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project_group
            artifactId project_artifact
            version project_version
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project_name
                    description project_description
                    url "https://github.com/$github_name/$project_name"
                    scm {
                        url "https://github.com/$github_name/$project_name"
                        connection "scm:git:git://github.com/$github_name/${project_name}.git"
                        developerConnection "scm:git:ssh:git@github.com:$github_name/${project_name}.git"
                    }
                    licenses {
                        license {
                            name project_license
                            url project_license_url
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id github_name
                            name dev_name
                            email dev_email
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = BINTRAY_USER
    key = BINTRAY_API_KEY
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project_name
        desc = project_description
        licenses = [project_license_tag]
        publicDownloadNumbers = true
        websiteUrl = "https://github.com/$github_name/$project_name"
        issueTrackerUrl = "https://github.com/$github_name/$project_name/issues"
        vcsUrl = "https://github.com/$github_name/${project_name}.git"
        githubRepo = "$github_name/$project_name"
        version {
            name = project_version
            vcsTag = project_version
            gpg {
                sign = true
            }
        }
    }
}